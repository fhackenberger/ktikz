isEmpty(QMAKECOMMAND): QMAKECOMMAND = qmake
isEmpty(LRELEASECOMMAND): LRELEASECOMMAND = lrelease
isEmpty(QCOLLECTIONGENERATORCOMMAND): QCOLLECTIONGENERATORCOMMAND = qcollectiongenerator

unix:!macx {
	isEmpty(PREFIX): PREFIX = /usr
	isEmpty(BIN_INSTALL_DIR): BIN_INSTALL_DIR = $${PREFIX}/bin
	isEmpty(DESKTOP_INSTALL_DIR): DESKTOP_INSTALL_DIR = /usr/share/applications
	isEmpty(MIME_INSTALL_DIR): MIME_INSTALL_DIR = /usr/share/mime/packages
	isEmpty(MAN_INSTALL_DIR): MAN_INSTALL_DIR = $${PREFIX}/share/man
	isEmpty(RESOURCES_INSTALL_DIR): RESOURCES_INSTALL_DIR = $${PREFIX}/share/qtikz
	isEmpty(TEMPLATE_EDITOR): TEMPLATE_EDITOR_DEFAULT = kwrite
}
macx { # untested
	CONFIG += link_prl
	isEmpty(BIN_INSTALL_DIR): BIN_INSTALL_DIR = /Applications
	isEmpty(RESOURCES_INSTALL_DIR): RESOURCES_INSTALL_DIR = Contents/Resources
	isEmpty(TEMPLATE_EDITOR): TEMPLATE_EDITOR_DEFAULT = kwrite
}
win32 {
	isEmpty(PREFIX) {
#		PREFIX = $$quote(C:/Program Files/QtikZ)
		PREFIX = "C:/QtikZ"
	}
	isEmpty(BIN_INSTALL_DIR): BIN_INSTALL_DIR = $${PREFIX}
	isEmpty(RESOURCES_INSTALL_DIR): RESOURCES_INSTALL_DIR = $${PREFIX}/data
	isEmpty(TEMPLATE_EDITOR): TEMPLATE_EDITOR_DEFAULT = notepad
}

isEmpty(DOCUMENTATION_INSTALL_DIR): DOCUMENTATION_INSTALL_DIR = $${RESOURCES_INSTALL_DIR}/documentation
isEmpty(TEMPLATES_INSTALL_DIR): TEMPLATES_INSTALL_DIR = $${RESOURCES_INSTALL_DIR}/templates
isEmpty(TRANSLATIONS_INSTALL_DIR): TRANSLATIONS_INSTALL_DIR = $${RESOURCES_INSTALL_DIR}/locale
